download-wasi-sdk os arch:
    #!/bin/sh
    set -e pipefail

    test -d .build/wasi-sdk || {
      WASI_OS={{ os }}
      WASI_ARCH={{ arch }}
      WASI_VERSION=29
      WASI_VERSION_FULL=${WASI_VERSION}.0
      mkdir -p .build/wasi-sdk
      curl -L https://github.com/WebAssembly/wasi-sdk/releases/download/wasi-sdk-${WASI_VERSION}/wasi-sdk-${WASI_VERSION_FULL}-${WASI_ARCH}-${WASI_OS}.tar.gz \
          > .build/wasi-sdk.tar.gz
      tar xvf .build/wasi-sdk.tar.gz -C .build/wasi-sdk --strip-components=1
    }

[macos]
install-wasi-sdk:
    just download-wasi-sdk macos $(arch)

[linux]
install-wasi-sdk:
    just download-wasi-sdk linux $(arch)

build: install-wasi-sdk
    WASI_SYSROOT="$(pwd)/.build/wasi-sdk/share/wasi-sysroot" \
    CC="$(pwd)/.build/wasi-sdk/bin/clang" \
    cargo build \
      -Zunstable-options \
      --target wasm32-wasip2 \
      --release \
      --artifact-dir "dist"

test:
    cargo test
